# Datalog grammar
-------------------------------------------------------------------------------

datalogProgram    ->    SCHEMES COLON scheme schemeList 
                        FACTS COLON factList 
                        RULES COLON ruleList 
                        QUERIES COLON query queryList

scheme            ->    ID LEFT_PAREN ID idList RIGHT_PAREN

schemeList        ->    scheme schemeList
                  ->    λ

idList            ->    COMMA ID idList
                  ->    λ

fact              ->    ID LEFT_PAREN STRING stringList RIGHT_PAREN PERIOD

factList          ->    fact factList
                  ->    λ

rule              ->    headPredicate COLON_DASH predicate predicateList PERIOD

ruleList          ->    rule ruleList
                  ->    λ

headPredicate     ->    ID LEFT_PAREN ID idList RIGHT_PAREN

predicate         ->    ID LEFT_PAREN parameter parameterList RIGHT_PAREN

predicateList     ->    COMMA predicate predicateList
                  ->    λ

parameter         ->    STRING
                  ->    ID
                  ->    expression

parameterList     ->    COMMA parameter parameterList
                  ->    λ

expression        ->    LEFT_PAREN parameter operator parameter RIGHT_PAREN

operator          ->    ADD
                  ->    MULTIPLY

query             ->    predicate Q_MARK

queryList         ->    query queryList
                  ->    λ

stringList        ->    COMMA STRING stringList
                  ->    λ


# Condensed list for checking

datalogProgram -> SCHEMES COLON scheme schemeList FACTS COLON factList RULES COLON ruleList QUERIES COLON query queryList
scheme -> ID LEFT_PAREN ID idList RIGHT_PAREN
schemeList -> scheme schemeList | λ
idList -> COMMA ID idList | λ
fact -> ID LEFT_PAREN STRING stringList RIGHT_PAREN PERIOD
factList -> fact factList | λ
rule -> headPredicate COLON_DASH predicate predicateList PERIOD
ruleList -> rule ruleList | λ
headPredicate -> ID LEFT_PAREN ID idList RIGHT_PAREN
predicate -> ID LEFT_PAREN parameter parameterList RIGHT_PAREN
predicateList -> COMMA predicate predicateList | λ
parameter -> STRING | ID | expression
parameterList -> COMMA parameter parameterList | λ
expression -> LEFT_PAREN parameter operator parameter RIGHT_PAREN
operator -> ADD | MULTIPLY
query -> predicate Q_MARK
queryList -> query queryList | λ
stringList -> COMMA STRING stringList | λ
